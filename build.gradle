buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies { 
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT' 
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}

apply plugin: 'forge'
apply plugin: 'maven-publish'


group = 'tterrag.wailaplugins'
archivesBaseName = "WAILAPlugins"
version = "MC${minecraft_version}-${mod_version}-${System.getenv().BUILD_NUMBER}"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = 'run'
    
    replace "@VERSION@", project.version
	replace "${toReplace}", "required-after:Forge@[required-after:ttCore@[${ttCore_version},);required-after:Waila;"
    replaceIn "WAILAPlugins.java"
}

repositories {
    maven { // ttCore
        name 'tterrag Repo'
        url "http://maven.tterrag.com/"
    }
	maven { // WAILA
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
	maven { // CCC/NEI (for WAILA)
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
}

dependencies {
    compile "tterrag.core:ttCore:${ttCore_version}"
	compile "mcp.mobius.waila:Waila:1.5.5_1.7.10"
    compile fileTree(dir: 'libs', include: '*.jar')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

jar {

}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

import de.undercouch.gradle.tasks.download.Download

task downloadZip(type: Download) {
    src 'http://assets.tterrag.com/WAILAPlugins/libs.zip'
    dest new File(buildDir, '1.0.zip')
    onlyIfNewer true
}

task downloadLibs(dependsOn: downloadZip, type: Copy) {
    from zipTree(downloadZip.dest)
    into file('libs')
}

tasks.build.dependsOn sourceJar, deobfJar
tasks.eclipse.dependsOn downloadLibs
tasks.idea.dependsOn downloadLibs
